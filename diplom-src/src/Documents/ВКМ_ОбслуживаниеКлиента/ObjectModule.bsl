
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Данные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "ВидДоговора, ВКМ_ДатаНачала, ВКМ_ДатаОкончания");
	Если Данные.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание") Тогда
		ОбщегоНазначения.СообщитьПользователю("Не выбран договор абонентского обслуживания");
		Отказ = Истина;
	КонецЕсли;
	Если Дата < Данные.ВКМ_ДатаНачала Или Дата > Данные.ВКМ_ДатаОкончания Тогда
		ОбщегоНазначения.СообщитьПользователю("Дата документа не входит в период действия договора");
		Отказ = Истина;
	КонецЕсли;
	
	ЧасоваяСтавка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_СтоимостьЧасаРаботы");
	ПроцентОтРабот = ПроцентОтРабот(Специалист);
	
	Если ПроцентОтРабот = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
		"Сотруднику %1 не указан процент от работ в условиях оплаты.", Специалист));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	//Регистр ВыполненныеРаботыКлиенту
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.ДобавитьПриход();
	Движение.Период = Дата;
	Движение.Клиент = Клиент;
	Движение.Договор = Договор;
	КоличествоЧасов = 0;
	Для Каждого ТекСтрока Из ВыполненныеРаботы Цикл
		КоличествоЧасов = КоличествоЧасов + ТекСтрока.ЧасыКОплатеКлиенту;
	КонецЦикла;
	Движение.КоличествоЧасов = КоличествоЧасов;
	Движение.СуммаКОплате = КоличествоЧасов * ЧасоваяСтавка;	
	
	//ВыполненныеСотрудникомРаботы
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
	Движение.Период = Дата;
	Движение.Сотрудник = Специалист;
	Движение.ЧасовКОплате = КоличествоЧасов;
	Движение.СуммаКОплате = КоличествоЧасов * ЧасоваяСтавка * ПроцентОтРабот / 100;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ДатаПроведенияРабот") <> ДатаПроведенияРабот
		Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ВремяНачалаРаботПлан") <> ВремяНачалаРаботПлан
		Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Специалист") <> Специалист Тогда
		ЗаказИзменен = Истина;
	Иначе
		ЗаказИзменен = Ложь;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЗаказИзменен", ЗаказИзменен);
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		ТекстСообщения = ТекстСообщения();
		Сообщение = СтрШаблон("Новый заказ №%1: " + ТекстСообщения, Номер);
		СозданиеЭлементаСправочника(Сообщение);
		
	ИначеЕсли ДополнительныеСвойства.ЗаказИзменен Тогда
		
		ТекстСообщения = ТекстСообщения();
		Сообщение = СтрШаблон("Изменен заказ №%1: " + ТекстСообщения, Номер);
		СозданиеЭлементаСправочника(Сообщение);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстСообщения()
	
	ТекстСообщения = СтрШаблон("исполнитель %1, время проведения: %2 с %3 по %4", Специалист, Формат(ДатаПроведенияРабот, "ДЛФ=D"), 
	Формат(ВремяНачалаРаботПлан, "ДЛФ=T"), Формат(ВремяОкончанияРаботПлан, "ДЛФ=T"));
	
	Возврат ТекстСообщения; 
	
КонецФункции

Процедура СозданиеЭлементаСправочника(Сообщение)
	
	Элемент = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
	Элемент.ТекстСообщения = Сообщение;
	Элемент.Записать();
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныепроцедурыИФункции

Функция ПроцентОтРабот(Сотрудник); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот
	               |ИЗ
	               |	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(, Сотрудник = &Сотрудник) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних";
	                
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() = 0 Тогда 
		Возврат Неопределено;
	Иначе  
		Возврат Результат[0].ПроцентОтРабот;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
